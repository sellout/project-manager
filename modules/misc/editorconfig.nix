### This configures basic cross-editor formatting.
###
### See https://editorconfig.org/ for more info, and to see if your editor
### requires a plugin to take advantage of it.
{
  config,
  lib,
  pkgs,
  ...
}:
with lib; let
  cfg = config.editorconfig;

  iniFormat = pkgs.formats.ini {};
in {
  meta.maintainers = with maintainers; [loicreynier];

  options.editorconfig = {
    enable = mkEnableOption "EditorConfig project configuration file";

    settings = mkOption {
      type = iniFormat.type;
      default = {};
      description = ''
        Configuration written to {file}`$PROJECT_ROOT/.editorconfig`.
        `root = true` is automatically added to the file,
        it must not be added here.
        See <https://editorconfig.org> for documentation.
      '';
      example = literalExpression ''
        {
          "*" = {
            charset = "utf-8";
            end_of_line = "lf";
            trim_trailing_whitespace = true;
            insert_final_newline = true;
            max_line_width = 78;
            indent_style = "space";
            indent_size = 4;
          };
        };
      '';
    };
  };

  config = mkIf (cfg.enable && cfg.settings != {}) {
    project.file.".editorconfig" = {
      persistence = "store";
      text = let
        renderedSettings = generators.toINIWithGlobalSection {} {
          globalSection = {root = true;};
          sections = cfg.settings;
        };
      in ''
        # Generated by Project Manager
        ${renderedSettings}
      '';
    };
  };
}
