{
  config,
  lib,
  pkgs,
  ...
}:
with lib; let
  cfg = config.programs.direnv;

  projectDirectory = config.project.projectDirectory;

  fileContents =
    (import ../lib/file-type.nix {
      inherit projectDirectory lib pkgs;
      commit-by-default = config.project.commit-by-default;
    })
    .fileContents;
in {
  meta.maintainers = [maintainers.sellout];

  options.programs.direnv = {
    enable = mkEnableOption "direnv, the environment switcher";

    package = mkOption {
      type = types.package;
      default = pkgs.direnv;
      defaultText = lib.literalMD "pkgs.direnv";
      description = ''
        Direnv package to install.
      '';
    };

    auto-allow = mkOption {
      type = types.bool;
      default = false;
      description = ''
        Whether running project-manager will implicitly run `direnv allow` for
        the user.
      '';
    };

    commit-envrc = mkOption {
      type = types.bool;
      default = true;
      description = ''
        Whether .envrc should be committed to the repo or only available
        locally. If this is false, then users will have to run
        `project-manager switch` before direnv will work.
      '';
    };

    envrc = mkOption {
      type = lib.types.lines;
      description = ''
        The contents of the .envrc file.
      '';
    };

    layoutDir = mkOption {
      type = lib.types.nullOr lib.types.str;
      default = "${config.xdg.cacheDir}/direnv";
      description = ''
        Where to store direnv‘s cache. If null, this won’t be set.
      '';
    };
  };

  config = mkIf cfg.enable {
    project.devPackages = [cfg.package];

    programs.git.ignores = ["/${cfg.layoutDir}/"];
    programs.mercurial.ignoresRooted = ["${cfg.layoutDir}/**"];
    programs.vale.excludes = mkIf (config.project.file.".envrc".persistence != "store") [
      "./${config.project.file.".envrc".target}"
    ];

    project = {
      activation.direnv = mkIf cfg.auto-allow (pm.dag.entryAfter ["onFilesChange"] ''
        ${pkgs.direnv}/bin/direnv allow
      '');

      file.".envrc" = {
        text =
          ''
            # This file was generated by Project Manager.
          ''
          + (
            if cfg.layoutDir == null
            then ""
            else ''
              direnv_layout_dir="$PWD/${cfg.layoutDir}"
            ''
          )
          + cfg.envrc;
        minimum-persistence = "worktree";
        ## See direnv/direnv#1160.
        broken-symlink = true;
        commit-by-default = cfg.commit-envrc;
      };
    };
  };
}
